# -*- python -*- =======================================================
# FILE:         SConstruct
# CONTENTS:     scons build script template
# AUTHOR:       steve AT k-hornz DOT de
# ======================================================================

import glob
import os
import re
import tarfile
import time

# ======================================================================
# setup
# ======================================================================

EnsureSConsVersion(0, 96)
EnsurePythonVersion(2, 3)
SConsignFile()

# ======================================================================
# constants
# ======================================================================

PACKAGE = 'xxx'
VERSION = '0.1'

PLATFORM = os.uname()[0].lower()
CPU = os.uname()[4].lower()

if PLATFORM == 'darwin':
    PLATFORM_SYMBOL = 'SC_DARWIN'
    PLUGIN_EXT = '.scx'
    DEFAULT_PREFIX = '/'
elif PLATFORM == 'linux':
    PLATFORM_SYMBOL = 'SC_LINUX'
    PLUGIN_EXT = '.so'
    DEFAULT_PREFIX = '/usr/local'
# elif PLATFORM == 'windows':
#     PLATFORM_SYMBOL = 'SC_WIN32'
#     PLUGIN_EXT = '.scx'
#     DEFAULT_PREFIX = '/'
else:
    print 'Unknown platform: %s' % PLATFORM
    Exit(1)

# ======================================================================
# util
# ======================================================================

def flatten_dir(dir):
    res = []
    for root, dirs, files in os.walk(dir):
        if 'CVS' in dirs: dirs.remove('CVS')
        for f in files:
            res.append(os.path.join(root, f))
    return res

def install_dir(env, src_dir, dst_dir, filter_re, strip_levels=0):
    nodes = []
    for root, dirs, files in os.walk(src_dir):
        src_paths = []
        dst_paths = []
        if '.svn' in dirs: dirs.remove('.svn')
        for d in dirs[:]:
            if filter_re.match(d):
                src_paths += flatten_dir(os.path.join(root, d))
                dirs.remove(d)
        for f in files:
            if filter_re.match(f):
                src_paths.append(os.path.join(root, f))
        dst_paths += map(
            lambda f:
            os.path.join(
            dst_dir,
            *f.split(os.path.sep)[strip_levels:]),
            src_paths)
        nodes += env.InstallAs(dst_paths, src_paths)
    return nodes

# ======================================================================
# command line options
# ======================================================================

opts = Options('scache.conf', ARGUMENTS)
opts.AddOptions(
    BoolOption('ALTIVEC',
               'Build with Altivec support', 1),
    ('CUSTOMCCFLAGS', 'Custom c compiler flags', ''),
    ('CUSTOMCXXFLAGS', 'Custom c++ compiler flags', ''),
    BoolOption('DEBUG',
               'Build with debugging information', 0),
    PathOption('DESTDIR',
               'Intermediate installation prefix for packaging', '/'),
    PathOption('PREFIX',
               'Installation prefix', DEFAULT_PREFIX),
    PathOption('SCSRCDIR',
               'SuperCollider source directory', '../SuperCollider3/')
    )

# ======================================================================
# basic environment
# ======================================================================

env = Environment(options = opts,
                  PACKAGE = PACKAGE,
                  VERSION = VERSION)
env.Append(
    CCFLAGS = env['CUSTOMCCFLAGS'],
    CXXFLAGS = env['CUSTOMCXXFLAGS'])

# defines and compiler flags
env.Append(
    CPPDEFINES = ['_REENTRANT', PLATFORM_SYMBOL],
    CCFLAGS = ['-Wno-unknown-pragmas'],
    CXXFLAGS = ['-Wno-deprecated']
    )

# debugging flags
if env['DEBUG']:
    env.Append(CCFLAGS = '-g')
else:
    env.Append(CPPDEFINES = ['NDEBUG'])

# platform specific
if CPU == 'ppc':
    env.Append(CCFLAGS = '-fsigned-char')
    if env['ALTIVEC']:
        env.Append(CCFLAGS = ['-maltivec', '-mabi=altivec'])

# finish
opts.Save('scache.conf', env)
Help(opts.GenerateHelpText(env))

# ======================================================================
# plugins
# ======================================================================

pluginEnv = env.Copy(SHLIBPREFIX = '', SHLIBSUFFIX = PLUGIN_EXT)
pluginEnv.Append(
    CPPPATH = map(lambda f: os.path.join(env['SCSRCDIR'], 'headers', f),
                  ['common', 'plugin_interface', 'server']))
if PLATFORM == 'darwin':
    # build a MACH-O bundle
    pluginEnv['SHLINKFLAGS'] = '$LINKFLAGS -bundle -flat_namespace -undefined suppress'
plugins = []

# example plugin
plugins.append(
    pluginEnv.SharedLibrary('plugins/xxx', ['src/xxx.cpp']))

env.Alias('plugins', plugins)
Default('plugins')

# ======================================================================
# installation directories
# ======================================================================

INSTALL_PREFIX = os.path.join('$DESTDIR', '$PREFIX')

if PLATFORM == 'darwin':
    DATA_DIR = os.path.join(INSTALL_PREFIX, 'Library', 'Application Support', 'SuperCollider')
    PLUGIN_DIR = os.path.join(env['SCSRCDIR'], 'build', 'plugins')
elif PLATFORM == 'linux':
    DATA_DIR = os.path.join(INSTALL_PREFIX, 'share', 'SuperCollider')
    PLUGIN_DIR = os.path.join(INSTALL_PREFIX, 'lib', 'SuperCollider', 'plugins')

HELP_DIR = os.path.join(DATA_DIR, 'Help', env['PACKAGE'])
LIB_DIR = os.path.join(DATA_DIR, 'Extensions', env['PACKAGE'])

HELP_FILE_RE = re.compile('.*\.(rtf(d)?|sc)$')
SC_FILE_RE = re.compile('.*\.sc$')

# ======================================================================
# installation
# ======================================================================

env.Alias('install-bin', env.Install(PLUGIN_DIR, plugins))

env.Alias('install-data', install_dir(
    env, 'help',
    HELP_DIR, HELP_FILE_RE, 1))

env.Alias('install-data', install_dir(
    env, 'lib',
    LIB_DIR, SC_FILE_RE, 1))

env.Alias('install', ['install-bin', 'install-data'])

# ======================================================================
# distribution
# ======================================================================

DIST_FILES = Split('''
AUTHORS
ChangeLog
COPYING
lib/xxx.sc
NEWS
README
SConstruct
src/xxx.cpp
TODO
''')

DIST_DIRS = Split('''
help
''')

def dist_paths():
    paths = DIST_FILES[:]
    for base in DIST_DIRS:
        for root, dirs, files in os.walk(base):
            if '.svn' in dirs: dirs.remove('.svn')
            for path in files:
                paths.append(os.path.join(root, path))
    paths.sort()
    return paths

def build_tar(env, target, source):
    paths = dist_paths()
    tarfile_name = str(target[0])
    tmp, ext = os.path.splitext(tarfile_name)
    tar_name = os.path.splitext(os.path.basename(tmp))[0]
    tar = tarfile.open(tarfile_name, "w:" + ext[1:])
    for path in paths:
        tar.add(path, os.path.join(tar_name, path))
    tar.close()

snapshot_tarball = PACKAGE + '-' + time.strftime("%Y%m%d", time.localtime()) + '.tar.bz2'
env.Alias('snapshot-dist', snapshot_tarball)
env.AlwaysBuild(env.Command(snapshot_tarball, None, build_tar))

release_tarball = PACKAGE + '-' + VERSION + '.tar.bz2'
env.Alias('release-dist', release_tarball)
env.AlwaysBuild(env.Command(release_tarball, None, build_tar))

env.Alias('dist', ['snapshot-dist'])

# ======================================================================
# cleanup
# ======================================================================

env.Clean('scrub',
          Split('config.log scache.conf .sconf_temp .sconsign.dblite') +
          glob.glob('*.tbz2'))

# ======================================================================
# EOF
