dnl ===================================================================
dnl file:	configure.in
dnl contents:	autoconf template
dnl author:	stefan kersten <steve@k-hornz.de>
dnl CVS:	$Id$
dnl ===================================================================

# initialize autoconf
AC_PREREQ(2.52)
AC_INIT(source/server/scsynth_main.cpp)
AC_CANONICAL_SYSTEM

# initialize automake
AM_INIT_AUTOMAKE(SuperCollider,3.0)
AM_CONFIG_HEADER(config.h)

# variables
SC_SYNTH_SHARED_VERSION_INFO="0:0:0"
AC_SUBST(SC_SYNTH_SHARED_VERSION_INFO)

SC_LANG_SHARED_VERSION_INFO="0:0:0"
AC_SUBST(SC_LANG_SHARED_VERSION_INFO)

SC_AUDIO_API=SC_AUDIO_API_JACK
AC_SUBST(SC_AUDIO_API)

SC_PLUGIN_DIR='"$(pkglibdir)/plugins"'
AC_SUBST(SC_PLUGIN_DIR)

SC_PLUGIN_EXT=".so"
AC_SUBST(SC_PLUGIN_EXT)

SC_PLUGIN_LOAD_SYM="load"
AC_SUBST(SC_PLUGIN_LOAD_SYM)

SC_LIBRARY_DIR='$(pkglibdir)/library'
AC_SUBST(SC_LIBRARY_DIR)

# check for programs
AC_PROG_CXX
AC_PROG_AWK
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET
AC_PROG_LD
AM_PROG_LIBTOOL

# additional configure flags
AC_ARG_ENABLE(debug,
AC_HELP_STRING([--enable-debug],[generate debugging symbols]))

AC_ARG_ENABLE(optimize,
AC_HELP_STRING([--enable-optimize],[enable compiler optimizations]))

# check for header files
AC_CHECK_HEADERS([errno.h strings.h unistd.h endian.h machine/endian.h])
AC_HEADER_STDC

# check for typedefs, structs, and compiler pecularities
AC_C_CONST
AC_C_INLINE
AC_TYPE_SIZE_T
AC_HEADER_TIME

# check for library functions
AC_FUNC_MALLOC
AC_FUNC_MMAP
AC_CHECK_FUNCS([bzero ftruncate gettimeofday memmove memset socket strcasecmp strchr strerror ntohs ntohl htons htonl])

# check for packages
pkg_modules="sndfile >= 1.0.0"
PKG_CHECK_MODULES(SNDFILE, [$pkg_modules])
AC_SUBST(SNDFILE_CFLAGS)
AC_SUBST(SNDFILE_LIBS)

pkg_modules="jack >= 0.40"
PKG_CHECK_MODULES(JACK, [$pkg_modules])
AC_SUBST(JACK_CFLAGS)
AC_SUBST(JACK_LIBS)

# check for libraries
AC_CHECK_LIB(pthread,pthread_create,lib_pthread="-lpthread",lib_pthread="")
if test "x$lib_pthread" = "x" ; then
    AC_MSG_ERROR([libpthread (POSIX threads) required])
fi

AC_CHECK_LIB(dl,dlopen,lib_dl="-ldl",lib_dl="")
if test "x$lib_dl" = "x" ; then
    AC_MSG_ERROR([libdl (dynamic link support) required])
fi

dnl AC_MSG_NOTICE([OS is $target_os])
dnl case "$target_os" in
dnl     linux* )
dnl 	;;
dnl esac # $target_os

# assemble compiler flags
CXXFLAGS="-Wall -DSC_LINUX -D_REENTRANT"

# add debug/optimization flags
if test "x$enable_debug" = "xyes" ; then
    AC_MSG_NOTICE([enabling debugging symbols])
    CXXFLAGS="$CXXFLAGS -g"
else
    AC_MSG_NOTICE([enabling compiler optimizations])
    CXXFLAGS="$CXXFLAGS -DNDEBUG"
    if test "x$enable_optimize" = "xyes" ; then
	CXXFLAGS="$CXXFLAGS -O6 -fomit-frame-pointer -fstrength-reduce -funroll-loops -fmove-all-movables -finline -finline-functions"
    fi
fi

# generate output files
AC_OUTPUT(
Makefile
headers/Makefile
headers/common/Makefile
headers/plugin_interface/Makefile
headers/server/Makefile
headers/lang/Makefile
source/Makefile
source/common/Makefile
source/plugins/Makefile
source/server/Makefile
source/lang/Makefile
source/lang/LangPrimSource/Makefile
source/lang/LangSource/Makefile
linux/Makefile
linux/libscsynth.pc
linux/libsclang.pc
)

dnl EOF ===============================================================
